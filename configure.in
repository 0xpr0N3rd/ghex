AC_PREREQ(2.53)
AC_INIT(ghex, 2.8.1, http://bugzilla.gnome.org/enter_bug.cgi?product=ghex)
AC_CONFIG_SRCDIR(src/ghex-window.c)
AM_INIT_AUTOMAKE(ghex, 2.8.1)
AM_CONFIG_HEADER(config.h)

AM_MAINTAINER_MODE

dnl The following line will have to be removed -- SnM
GNOME_COMMON_INIT

AC_PROG_INTLTOOL([0.30])

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AM_PROG_LIBTOOL

AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

LIBGTKHEX_RELEASE=1.0
AC_SUBST(LIBGTKHEX_RELEASE)
LIBGTKHEX_VERSION=${LIBGTKHEX_RELEASE}.0
LIBGTKHEX_RELEASE_STRING=gtkhex-${LIBGTKHEX_RELEASE}
AC_SUBST(LIBGTKHEX_VERSION)
AC_DEFINE_UNQUOTED(LIBGTKHEX_RELEASE_STRING, "$LIBGTKHEX_RELEASE_STRING",
                   [GtkHex library release string.])

PKG_CHECK_MODULES(LIBGTKHEX,
		  gtk+-2.0 >= 2.4.0	\
		  atk >= 1.0.0)

LIBGTKHEX_CFLAGS="$LIBGTKHEX_CFLAGS"

AC_SUBST(LIBGTKHEX_LIBS)
AC_SUBST(LIBGTKHEX_CFLAGS)

PKG_CHECK_MODULES(GHEX,
		  gtk+-2.0 >= 2.4.0 		\
		  libgnomeui-2.0 >= 2.6.0	\
		  libgnomeprintui-2.2)

GHEX_CFLAGS="$GHEX_CFLAGS"

AC_SUBST(GHEX_CFLAGS)
AC_SUBST(GHEX_LIBS)

PKG_CHECK_MODULES(GAILUTIL, gail >= 0.17)
AC_SUBST(GAILUTIL_CFLAGS)
AC_SUBST(GAILUTIL_LIBS)

dnl =================================================================
dnl Gettext stuff
dnl =================================================================
GETTEXT_PACKAGE=ghex-2.0
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Gettext package name.])
ALL_LINGUAS="am az be bg ca cs da de el en_CA en_GB es et eu fi fr ga gl hr hu id it ja ko ml ms nl nn no pa pl pt pt_BR ro ru rw sk sl sq sr sr@Latn sv tr uk vi wa zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT

AC_CHECK_LIB(popt, poptGetArgs, [POPT_LIBS="-lpopt"],
AC_MSG_ERROR([popt is required to build ghex.
You can download the latest version from ftp://ftp.rpm.org/pub/rpm/dist/rpm-4.0.x/]))
AC_SUBST(POPT_LIBS)

AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

dnl A switch to enable *_DISABLE__DEPRECATED flags
AC_ARG_ENABLE(deprecations,
              [AC_HELP_STRING([--enable-deprecations],
                              [warn about deprecated usages [default=no]])],
              GHEX_DEPRECATED_CFLAGS="-DG_DISABLE_DEPRECATED -DGDK_DISABLE_DEPRECATED -DGTK_DISABLE_DEPRECATED -DGNOME_DISABLE_DEPRECATED -DPANGO_DISABLE_DEPRECATED",
              GHEX_DEPRECATED_CFLAGS="")
AC_SUBST(GHEX_DEPRECATED_CFLAGS)

AC_ARG_ENABLE(debug,
              [  --enable-debug      Enables extra debugging output], AC_DEFINE(ENABLE_DEBUG, 1, [Should verbose debugging output be enabled.]))

dnl Checking for gconftool-2
AC_PATH_PROG(GCONFTOOL, gconftool-2, no)

if test "x$GCONFTOOL" = xno; then
	AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi

AM_GCONF_SOURCE_2

AC_OUTPUT([
Makefile
po/Makefile.in
src/Makefile
help/Makefile
help/C/Makefile
help/ja/Makefile
help/de/Makefile
help/es/Makefile
help/fr/Makefile
help/it/Makefile
help/sv/Makefile
help/ko/Makefile
help/zh_CN/Makefile
help/zh_TW/Makefile
omf-install/Makefile
ghex.spec
gtkhex.pc])
